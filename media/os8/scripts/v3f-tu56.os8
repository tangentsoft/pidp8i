# Builds a bootable dectape image with pieces taken from:
# v3d.rk05, the os8v3d device driver distribution DECtape,
# and the os8v3f-build rk05.
# Building a bootable TD image is tricky, because our boot disk
# is configured with TC08, not TD8E

# For TD8E we run BUILD 3 times:
# First from SYS to change switch from TC08 to TD8E
# Then from RKB1 to build the TD8E head,
# Finally again from SYS to undo the first mess.

mount rk0 $bin/v3d.rk05 required
mount rk1 $bin/v3f-made.rk05 required
mount dt1 $os8mi/al-4712c-ba-os8-v3d-2.1978.tu56 readonly required

# We do tc08 by default
enable tc08

begin enabled td12k
enable td8e
end enabled td12k

begin enabled tdrom
enable td8e
end enabled tdrom

begin enabled td8e
# td12k and tdrom, the td8e options unset tc08.
disable tc08
boot rk0

os8 SET SYS NO INIT

begin build SYS:BUILD
# TD8E non-system handler.
# Comment out if we run out of room in BUILD.
LOAD DTA1:TD8EA.BN
DELETE DTA0,DTA1
INSERT TD8A,DTA0,DTA1
BOOT
end build

configure tape td

mount td0 $os8mo/v3f-td12k.tu56 new
mount td1 $os8mi/al-4712c-ba-os8-v3d-2.1978.tu56 readonly required
end enabled td8e

# Options decide what our output tape will be called.
begin enabled tc08
mount dt0 $os8mo/v3f-tc08.tu56 new
end enabled tc08

boot rk0

os8 COPY DSK:<RKB1:OS8.BN,CD.BN

begin build RKB1:BUILD
LOAD DTA1:RK8ESY.BN
LOAD DTA1:RK8ENS.BN
LOAD DTA1:PT8E.BN

# TD8E non-system handler.
LOAD DTA1:TD8EA.BN

begin enabled tc08
LOAD DTA1:TC08SY.BN
LOAD DTA1:TC08NS.BN
end enabled tc08

# TD8E systems with 12K uncomment next line.
begin enabled td12k
LOAD DTA1:TD8ESY.BN
end enabled td12k
# TD8E systems with ROM uncomment next line.
begin enabled tdrom
LOAD DTA1:ROMMSY.BN
end enabled tdrom

# Additional TD8E non-system devices
# Units 2 and 3.
# LOAD DTA1:TD8EB.BN
# Units 4 and 5.
# LOAD DTA1:TD8EC.BN
# Units 6 and 7.
# LOAD DTA1:TD8ED.BN

# I think we want the RKB1 version of KL8E.BN
LOAD DTA1:KL8E.BN
LOAD DTA1:LPSV.BN
LOAD RKB1:RXSY2.BN
LOAD RKB1:RXNS.BN

INSERT PT8E,PTR
INSERT PT8E,PTP
INSERT KL8E,TTY
INSERT LPSV,LPT

begin enabled tc08
SYSTEM TC08
INSERT TC08,DTA0
INSERT TC,DTA1
DSK TC08:DTA0
end enabled tc08

# Un-comment next 4 lines for TD8E 12K System
begin enabled td12k
SYSTEM TD8E
INSERT TD8E,DTA0
INSERT TD8E,DTA1
DSK TD8E:DTA0
end enabled td12k

# Un-comment next 4 lines for TD8E ROM System
begin enabled tdrom
SYSTEM ROM
INSERT ROM,DTA0
INSERT ROM,DTA1
DSK ROM:DTA0
end enabled tdrom

INSERT RX02,RXA0

INSERT RK05,RKA0,RKB0
INSERT RK05,RKA1,RKB1
INSERT RK05,RKA2,RKB2

BUILD DSK:OS8.BN DSK:CD.BN
BOOT
end build

os8 SAVE DTA0 BUILD.SV

include $media/os8/scripts/v3f-tape-copyin.os8

begin enabled td8e
# Put system image back to TC08
# The saved BUILD will just work.
boot rk0
begin build SYS:BUILD.SV
BOOT
end build
os8 SET SYS INIT

boot td0
end enabled td8e

begin enabled tc08
boot dt0
end enabled tc08

os8 R CCL
