########################################################################
# auto.def - Configure file for the PiDP-8/I software build system,
#            based on autosetup.
########################################################################

define defaultprefix /opt/pidp8i

use cc

options {
	serial-mod => "build simulator to expect the PCB serial mods"
}

if {[opt-bool serial-mod]} {
	msg-result "The simulator will expect the serial mods to the Pi and PiDP-8/I PCBs."
	define SERIALSETUP
}

cc-check-includes time.h
cc-check-functions clock_nanosleep nanosleep usleep

# We need to find an install(1) type program that supports -D.  The
# Raspberry Pi OSes typically used with the PiDB-8/I board do have this,
# but this package also runs on non-Linux OSes (e.g. for testing on a
# desktop Mac) so make sure we've got a suitable implementation.  The
# ginstall name is typical on non-Linux systems where GNU Coreutils was
# installed alongside the core OS utilities.
if {[cc-check-progs ginstall]} {
	define INSTALL ginstall
} elseif {[cc-check-progs install]} {
	if {[catch {exec install -D -d . >& /dev/null} result] == 0} {
		define INSTALL install
	} else {
		user-error "install(1) does not support -D; install GNU Coreutils."
	}
} else {
	user-error "No install(1) type program found; install GNU Coreutils."
}
msg-result "Found GNU install(1) program as [get-define INSTALL]."

# Also find GNU readlink in the same way
if {[cc-check-progs greadlink]} {
	set rlprg greadlink
} elseif {[cc-check-progs readlink]} {
	if {[catch {exec readlink -f . >& /dev/null} result] == 0} {
		set rlprg readlink
	} else {
		user-error "readlink(1) does not support -D; install GNU Coreutils."
	}
} else {
	user-error "No readlink(1) type program found; install GNU Coreutils."
}
msg-result "Found GNU readlink(1) as $rlprg."

# Absolutize the prefix, for generated scripts, then build variants
define ABSPREFIX [exec $rlprg -f [get-define prefix]]
define BOOTDIR "[get-define ABSPREFIX]/share/boot"
define MEDIADIR "[get-define ABSPREFIX]/share/media"

# If we have a PAL-III type assembler here, build the examples, too
if {[cc-check-progs palbart]} {
	define EXAMPLES {examples/add.pt}
} else {
	define EXAMPLES {}
}

make-config-header src/config.h \
	-auto {ENABLE_* HAVE_* PACKAGE_* SIZEOF_*} \
	-bare {SERIALSETUP}
make-template Makefile.in
make-template boot/0.script.in
make-template boot/2.script.in
make-template boot/3.script.in
make-template boot/4.script.in
make-template boot/6.script.in
make-template boot/7.script.in
make-template etc/pidp8i-init.in
